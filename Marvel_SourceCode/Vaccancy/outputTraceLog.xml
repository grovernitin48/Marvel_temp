<LocalDescRef version="1.1">
  <name>outputTraceLog</name>
  <type>6</type>
  <appObjectDesc class="SCRIPT">
    <type>0</type>
    <scriptText>;-------------------------------------------------------------------------------
;  トレースログを出力する
;
; @param channelKind       チャネル区分
; @param logLevel          ログレベル
; @param recordLocator     レコードロケータ
; @param pnrIdentification PNR識別
; @param processName       処理名
; @param freeText          フリーテキスト
; @param additionalText    追加フリーテキスト
; @author Watanabe Shinji
; @author Keita Okada
;
; Copyright (C) All Nippon Airways Co., Ltd.  All rights reserved.
;-------------------------------------------------------------------------------
name_arguments    #1 'channelKind'
name_arguments    #2 'logLevel'
name_arguments    #3 'recordLocator'
name_arguments    #4 'pnrIdentification'
name_arguments    #5 'processName'
name_arguments    #6 'freeText'
name_arguments    #7 'additionalText'

build_local_data_item  '_dateTime'
build_local_data_item  '_language'
build_local_data_item  '_channel'
build_local_data_item  '_sessionId'
build_local_data_item  '_userId'
build_local_data_item  '_officeCode'
build_local_data_item  '_dutyCode'
build_local_data_item  '_logMessage'
build_local_data_item  '_freeText'
build_local_data_item  '_dmsLogFlag'
build_local_data_item  '_logger'

; 国内用ログ出力処理判定
call 'getChannel' #1=_dmsLogFlag

set _freeText freeText
if '' &lt;&gt; additionalText
    concat _freeText additionalText
end_if

; 日時取得
call 'getCurrentDateTime' #1=_dateTime

;　Qik表示言語取得
get_language _language

; チャネル取得
if (channelKind = '0')
    set _channel channelDataItem
else
    set _channel 'CMN'
end_if

if _dmsLogFlag = 'DMS' | _dmsLogFlag = 'REF_DMS'
    ; 国内情報取得
    call 'callScript' #1='DmsSmartUtil:dmsGetCommonLogInfo' #2=_sessionId #3=_userId #4=_officeCode #5=_dutyCode
else
    ; 国際情報取得
    ; セッションID取得
    set _sessionId sessionIdDataitem
    
    ; ユーザID取得
    set _userId authenticateUserIdDataitem
    
    ; オフィスコード取得
    set _officeCode authenticateSourceOffice
    
    ; Dutyコード取得
    set _dutyCode authenticateDutyIdentifier
end_if

concat _logMessage _dateTime ',' _language ',' _channel ',' _sessionId ',' logLevel ',' _userId ',' _officeCode ',' \
    _dutyCode ',' recordLocator ',' pnrIdentification ',' processName ',' _freeText

if SmartUtil:fromRobotDataitem = 'Y'
    ; 対象Robotのプロパティファイルに定義されているID(wbs.login.id)でログの出し分けを行う
    ; 対象Robotは、KL、ScheduleChange、Refund、Upgrade、StatusChange、CreateNCP、AlteaHealthCheck
    switch _userId
    case 'QIKRBTQUE'
        set _logger 'jp.co.ana.smart.traceLoggerRbtque'
    case 'QIKRBTREI'
        set _logger 'jp.co.ana.smart.traceLoggerRbtrei'
    case 'QIKRBTRFD'
        set _logger 'jp.co.ana.smart.traceLoggerRbtrfd'
    case 'QIKRBTUPG'
        set _logger 'jp.co.ana.smart.traceLoggerRbtupg'
    case 'QIKRBTSSR'
        set _logger 'jp.co.ana.smart.traceLoggerRbtssr'
    case 'QIKRBTNCP'
        set _logger 'jp.co.ana.smart.traceLoggerRbtncp'
    case 'QIKRBTAHC'
        set _logger 'jp.co.ana.smart.traceLoggerRbtahc'
    default
        set _logger 'jp.co.ana.smart.traceLogger'
    end_switch
else
    set _logger 'jp.co.ana.smart.traceLogger'
end_if
call 'outputLog_js' #1=_logger #2=_logMessage #3=logLevel #4='Y'

if (_channel = 'INT' | _channel = 'REF_INT' ) &amp; logLevel = 'ERROR'
    call 'SmartUtil:outputErrorLog' #1='0' #2=processName #3='999999' #4=_freeText #5=recordLocator #6=pnrIdentification
end_if
</scriptText>
    <javaScriptMode>0</javaScriptMode>
    <created class="com.poet.rt.db.tracked.TrackedDate">2013-06-21 10:52:43.0 GMT</created>
    <lastSaved class="com.poet.rt.db.tracked.TrackedDate">2019-05-09 06:19:52.0 GMT</lastSaved>
  </appObjectDesc>
</LocalDescRef>